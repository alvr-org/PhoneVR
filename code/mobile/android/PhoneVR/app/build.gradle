apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        releaseconfig {
            storeFile file(keystoreProperties['storeFile'])
            storePassword System.getenv("Store")
            keyAlias keystoreProperties['keyAlias']
            keyPassword System.getenv("Key")
        }
    }
    android.ndkVersion "21.3.6528147"
    compileSdkVersion 30
    defaultConfig {
        applicationId "viritualisres.phonevr"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode Integer.valueOf(System.env.APPVEYOR_BUILD_NUMBER ?: 0)
        versionName System.getenv("APPVEYOR_PVR_TAG")
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                arguments ""
            }
        }

        defaultPublishConfig 'release'
        publishNonDefault true
        archivesBaseName = "PhoneVR-v$versionCode($versionName)"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.releaseconfig
        }
        debug {
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled false
            useProguard false
            externalNativeBuild {
                cmake {
                    cppFlags += "-D_DEBUG=1"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    flavorDimensions "arch"
    productFlavors {
        arm7 {
            dimension "arch"
            ndk.abiFilter 'armeabi-v7a'
        }
        arm8 {
            dimension "arch"
            ndk.abiFilters 'arm64-v8a'
        }
        x86 {
            dimension "arch"
            ndk.abiFilter 'x86'
        }
    }
    // CompileOptions required for ARCA
    compileOptions
    {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    //noinspection GradleDynamicVersion
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    //noinspection GradleDynamicVersion
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.google.vr:sdk-base:1.101.0'

    def acraVersion = '5.7.0'
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"

    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
}
